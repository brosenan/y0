* [$c_0$ is not C](#$c_0$-is-not-c)
  * [Types](#types)
    * [Types of Results of Arithmetic Operators](#types-of-results-of-arithmetic operators)
  * [Expressions](#expressions)
    * [Left Hand Expressions](#left-hand-expressions)
  * [Statements](#statements)
  * [Function Definitions](#function-definitions)
```clojure
(ns c0)

```
# $c_0$ is not C

## Types
```clojure
(all [t]
     (type t ! t "is not a type"))

(all [t]
     (type [:type t]) <-
     (type t))

(all []
     (type "int"))
(all []
     (type "float"))
(all [t]
     (type [:pointer_type t]))

```
### Types of Results of Arithmetic Operators
```clojure
(all [ta tb t]
     (arith-type ta tb ta) <-
     (arith-type ta)
     (= ta tb ! "Type mismatch in arithmetic operator:" ta "vs." tb))

(all [t]
     (arith-type t ! "Cannot do arithmetics on type" t))
(all [] (arith-type "int"))
(all [] (arith-type "float"))
```
## Expressions
```clojure
(all [x t]
     (typeof x t ! "Invalid expression" x))
(all [x t]
     (typeof [:expr x] t) <-
     (typeof x t))
(all [n]
     (typeof [:int n] "int"))
(all [n]
     (typeof [:float n] "float"))
(all [x t]
     (typeof [:addressof x] [:pointer_type t]) <-
     (addressable x)
     (typeof x t))
(all [x t]
     (typeof [:deref x] t) <-
     (exist [t']
            (typeof x t')
            (= t' [:pointer_type t] ! "Expected" x "to be a pointer but"
               t' "was inferred")))
(all [a b t]
     (typeof [:add a b] t) <-
     (exist [ta]
            (typeof a ta)
            (exist [tb]
                   (typeof b tb)
                   (arith-type ta tb t ! "when adding" a "and" b))))

```
### Left Hand Expressions
```clojure
(all [x]
     (lexpr x ! x "is not a left-hand expression"))
(all [x]
     (lexpr [:expr x]) <-
     (lexpr x))
(all [p]
     (lexpr [:deref p]))

 ;; ### Addressable Expressions
(all [x]
     (addressable x ! x "does not have an address"))

```
## Statements

```clojure
(all [stms]
     (statements stms ! "Expected a vector of statements. Found" stms))
(all [stm stms]
     (statements [stm & stms]) <-
     (statement stm stms))
(all []
     (statements []))

(all [stm stms]
     (statement stm stms ! "Invalid statement" stm))

(all [t var val stms]
     (statement [:vardef t var val] stms) <-
     (type t)
     (exist [t']
            (typeof val t')
            (= t t' ! "Type mismatch. Variable" var "was given type" t
               "but was assign value of type" t'))
     (given (declvar var t)
            (statements stms)))

(all [var val stms]
     (statement [:assign var val] stms) <-
     (lexpr var)
     (exist [t]
            (typeof var t)
            (exist [t']
                   (typeof val t')
                   (= t t' ! "Type mismatch. Variable" var "was given type" t
                      "but was assign value of type" t')))
     (statements stms))

(all [var t]
     (declvar var t) =>
     (all [] (typeof var t))
     (all [] (lexpr var))
     (all [] (addressable var)))

```
## Function Definitions
```clojure
(all [l]
     (arg-defs l ! l "is not an arg-def"))

(all [arg args]
     (arg-defs [arg & args]) <-
     (arg-def arg)
     (arg-defs args))
(all []
     (arg-defs []))

(all [def]
     (arg-def def ! def "is not an argument definition"))
(all [t name]
     (arg-def [:arg_def t name]) <-
     (type t))
(all []
     (arg-def :arg_defs))

(all [t name args stms]
     [:func_def t name args & stms] =>
     (assert
      (type t)
      (arg-defs args)
      (given (declargs args)
             (statements stms))))

(all [arg args]
     (declargs [arg & args]) =>
     (declarg arg)
     (declargs args))

(all [t var]
     (declarg [:arg_def t var]) =>
     (declvar var t))
```

